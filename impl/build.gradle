apply plugin: 'java'
apply plugin: 'antlr'

dependencies {
	antlr 'org.antlr:antlr4'

	compile project( ':api' )

	compile 'org.slf4j:slf4j-api'

	compile 'org.apache.commons:commons-lang3'
	compile 'commons-io:commons-io'

	compile 'org.yaml:snakeyaml'
	compile 'org.ini4j:ini4j'

	compile 'com.google.guava:guava'
	compile 'com.google.code.gson:gson'

	compile 'io.github.lukehutch:fast-classpath-scanner'
	compile 'ru.vyarus:generics-resolver:2.0.1'

	testCompile 'org.antlr:antlr4-runtime'
	testCompile 'junit:junit'
	testCompile 'org.hamcrest:hamcrest-library'
	testCompile 'io.leangen.geantyref:geantyref'

	testRuntime'org.slf4j:jul-to-slf4j'
	testRuntime 'org.slf4j:jcl-over-slf4j'
	testRuntime 'org.slf4j:log4j-over-slf4j'
	testRuntime 'ch.qos.logback:logback-classic'
}

configurations {
	testArtifacts.extendsFrom testRuntime
}

artifacts {
	testArtifacts testJar
}

tasks.withType( AntlrTask ).all { t ->
	eclipseClasspath {
		dependsOn t
	}
}

task( cleanLog, type: Delete ) {
	delete '**/*.log'
	outputs.upToDateWhen { false }
}

test {
	dependsOn cleanLog
}

task( grun, type: JavaExec ) {
	main = 'org.antlr.v4.gui.TestRig'
	classpath += sourceSets.test.output
	classpath += sourceSets.test.runtimeClasspath
	classpath += configurations.antlr
	args += [ 'ascelion.cdi.conf.Expression', 'root' ]
	args += [ '-tokens', '-trace', '-diagnostics', '-SLL', '-tree']
	
	if( System.getProperty( "stdin" ) == '' ) {
		standardInput = System.in
	}
	else {
		args += fileTree( dir: 'src/test/resources', include: 'expression-??.txt' ).sort()
	}
}

