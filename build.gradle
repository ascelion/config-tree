buildscript {
	repositories {
		mavenCentral()
		jcenter()
	}
	dependencies {
		classpath 'pl.allegro.tech.build:axion-release-plugin:1.11.0'
		classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
	}
}

apply plugin: 'pl.allegro.tech.build.axion-release'

scmVersion {
	ignoreUncommittedChanges = false
}

group = 'ascelion.config'
version = scmVersion.version

allprojects {
	ext {
		publishName = rootProject.name
		publishBase = 'https://github.com/ascelion'
		publishRepo = 'git@github.com:ascelion'
	}

	repositories {
		mavenLocal()
		mavenCentral()
		jcenter()
	}

	apply plugin: 'lifecycle-base'
	apply plugin: 'eclipse'

	clean {
		delete 'bin', 'target', '*.log'
	}

	cleanEclipse {
		delete '.settings', '.classpath', '.project'
	}

	defaultTasks 'clean', 'build'
}

subprojects { sub ->
	group = rootProject.group
	version = rootProject.version

	apply plugin: 'java-library'

	sourceCompatibility = '8'
	targetCompatibility = '8'

	sourceSets {
		all {
			output.resourcesDir = output.classesDirs[0]
		}
	}

	tasks.withType( JavaCompile ) {
		options.compilerArgs << '-parameters'
	}
	tasks.withType( AbstractArchiveTask ) {
		duplicatesStrategy = 'exclude'
		archiveVersion = ''
	}

	dependencies {
		constraints {
			implementation 'jakarta.enterprise:jakarta.enterprise.cdi-api:2.0.2'
			implementation 'jakarta.annotation:jakarta.annotation-api:1.3.5'
			implementation 'org.eclipse.microprofile.config:microprofile-config-api:1.3'

			implementation 'org.apache.commons:commons-lang3:3.9'
			implementation 'org.apache.commons:commons-text:1.8'

			implementation 'org.slf4j:slf4j-api:1.7.28'
			implementation 'ch.qos.logback:logback-classic:1.2.3'

			implementation 'ascelion.public:cdi-utils:0.2.1'

			implementation 'io.smallrye:smallrye-config:1.3.9'
			implementation 'org.apache.geronimo.config:geronimo-config-impl:1.2.2'
			implementation 'io.helidon.microprofile.config:helidon-microprofile-config:1.3.1'

			implementation 'org.yaml:snakeyaml:1.25'
			implementation 'io.leangen.geantyref:geantyref:1.3.9'
		}
	}

	eclipse.project.name = "$rootProject.name${project.path.replace(':', '-')}"
	eclipse.classpath.downloadSources = true
}

apply from: 'lombok.gradle'
apply from: 'tests.gradle'
apply from: 'jacoco.gradle'
apply from: 'publish.gradle'
