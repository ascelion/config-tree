buildscript {
	dependencies {
		classpath 'ascelion.gradle.plugins:gradle-ascelion-plugin:1.1.1-SNAPSHOT'
		classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.1'
	}
}

allprojects {
	group = 'ascelion.shared'

	apply plugin: 'ascelion'
}

subprojects {
	apply plugin: 'java'

	tasks.withType( JavaCompile ) {
		options.compilerArgs << '-parameters'
	}
	plugins.withType( JavaPlugin ) {
		sourceSets.all {
			dependencies.add( it.compileOnlyConfigurationName, 'org.projectlombok:lombok' )
		}
	}

	def fullName = "${rootProject.name}${path.replace(':', '-')}"

	eclipse.project.name = fullName
	ascelion.java.main.artifactId = fullName
}

task( name: 'test-report', type: TestReport) { rep ->
	destinationDir = file("$buildDir/reports/allTests")

	subprojects { sub ->
		sub.tasks.withType( Test ) { tst ->
			ignoreFailures = true
			reports.html.enabled = false

			rep.reportOn tst
		}
	}
}
